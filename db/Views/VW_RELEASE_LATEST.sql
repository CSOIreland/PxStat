SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

-- =============================================
-- Author:		Paulo Patricio
-- Create date: 9 Nov 2018
-- Description:	Gets the Latest Revision for the Latest Version of each Matrix
-- =============================================
CREATE
	OR

ALTER VIEW VW_RELEASE_LATEST
AS
SELECT RLS_ID VRL_RLS_ID,
	MTR_ID VRL_MTR_ID
FROM TD_MATRIX
INNER JOIN TD_RELEASE
	ON RLS_ID = MTR_RLS_ID
		AND RLS_DELETE_FLAG = 0
INNER JOIN (
	SELECT MTR.MTR_CODE LAST_MTR_CODE,
		RLS.RLS_VERSION AS MAX_RLS_VERSION,
		Max(RLS.RLS_REVISION) AS MAX_RLS_REVISION
	FROM TD_MATRIX MTR
	INNER JOIN TD_RELEASE RLS
		ON RLS.RLS_ID = MTR.MTR_RLS_ID
			AND MTR.MTR_DELETE_FLAG = 0
			AND RLS.RLS_DELETE_FLAG = 0
	INNER JOIN (
		SELECT MTR_INNER.MTR_CODE,
			MAX(RLS_INNER.RLS_VERSION) MAX_RLS_VERSION
		FROM TD_MATRIX MTR_INNER
		INNER JOIN TD_RELEASE RLS_INNER
			ON RLS_INNER.RLS_ID = MTR_INNER.MTR_RLS_ID
				AND MTR_INNER.MTR_DELETE_FLAG = 0
				AND RLS_INNER.RLS_DELETE_FLAG = 0
		GROUP BY MTR_INNER.MTR_CODE
		) a
		ON a.MTR_CODE = MTR.MTR_CODE
			AND a.MAX_RLS_VERSION = RLS.RLS_VERSION
	GROUP BY MTR.MTR_CODE,
		RLS.RLS_VERSION
	) last_mtr_release
	ON last_mtr_release.MAX_RLS_REVISION = RLS_REVISION
		AND last_mtr_release.MAX_RLS_VERSION = RLS_VERSION
		AND last_mtr_release.LAST_MTR_CODE = MTR_CODE
WHERE MTR_DELETE_FLAG = 0
GO

--select * from VW_RELEASE_LATEST
