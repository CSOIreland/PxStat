SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

-- =============================================
-- Author:	Paulo Patricio
-- Read date: 12 Nov 2018
-- Description:	Reads record(s) from 
-- exec Workflow_ReadWorkInProgress_dev 'okeeffene'
-- =============================================
CREATE
	OR

ALTER PROCEDURE Workflow_ReadWorkInProgress @CcnUsername NVARCHAR(256)
	,@RlsCode INT = NULL
AS
BEGIN
	SET NOCOUNT ON;

	DECLARE @GroupUserHasAccess TABLE (GRP_ID INT NOT NULL);

	INSERT INTO @GroupUserHasAccess
	EXEC Security_Group_AccessList @CcnUsername

	SELECT RLS_CODE RlsCode
		,MTR_CODE MtrCode
		,GRP_CODE GrpCode
		,GRP_NAME GrpName
		,CCN_USERNAME CcnUsername
		,RQS_VALUE AS Request
		,max(DHT_DATETIME) DhtDatetime
	FROM TD_MATRIX
	INNER JOIN TD_RELEASE
		ON RLS_ID = MTR_RLS_ID
			AND RLS_DELETE_FLAG = 0
	INNER JOIN VW_RELEASE_WIP
		ON VRW_RLS_ID = RLS_ID
			AND VRW_MTR_ID = MTR_ID
	INNER JOIN @GroupUserHasAccess g
		ON g.GRP_ID = RLS_GRP_ID
	INNER JOIN TD_GROUP GRP
		ON GRP.GRP_ID = RLS_GRP_ID
			AND GRP_DELETE_FLAG = 0
	INNER JOIN TM_AUDITING_HISTORY
		ON MTR_DTG_ID = DHT_DTG_ID
	INNER JOIN TS_AUDITING_TYPE
		ON DHT_DTP_ID = DTP_ID
			AND DTP_CODE  = 'CREATED'
	INNER JOIN TD_ACCOUNT
		ON DHT_CCN_ID = CCN_ID
			AND CCN_DELETE_FLAG = 0
	LEFT JOIN TD_WORKFLOW_REQUEST
		ON RLS_ID = WRQ_RLS_ID
	LEFT JOIN TS_REQUEST
		ON WRQ_RQS_ID = RQS_ID
	WHERE MTR_DELETE_FLAG = 0
		AND (
			@RlsCode IS NULL
			OR @RlsCode = RLS_CODE
			)
	GROUP BY RLS_CODE
		,MTR_CODE
		,GRP_CODE
		,GRP_NAME
		,CCN_USERNAME
		,RQS_VALUE
	ORDER BY MTR_CODE
END
GO


