SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

-- =============================================
-- Author:		Paulo Patricio
-- Read date: 22 Oct 2018
-- Description:	Reads one record from the TD_MATRIX and TD_Release table for a given language
-- exec Data_Matrix_Read 3, 'en', 'okeeffene'
-- =============================================
CREATE
	OR

ALTER PROCEDURE Data_Matrix_Read @RlsCode INT
	,@LngIsoCode CHAR(2)
	,@CcnUsername VARCHAR(256)
AS
BEGIN
	SET NOCOUNT ON;

	DECLARE @GroupUserHasAccess TABLE (GRP_ID INT NOT NULL);

	INSERT INTO @GroupUserHasAccess
	EXEC Security_Group_AccessList @CcnUsername

	DECLARE @maxDate DATETIME

	SELECT MTR_CODE MtrCode
		,MTR_TITLE MtrTitle
		,MTR_NOTE MtrNote
		,MTR_OFFICIAL_FLAG MtrOfficialFlag
		,MTR_INPUT MtrInput
		,CCN_CRT.CCN_USERNAME CcnUsernameCreate
		,DHT_CREATE.DHT_DATETIME DtgCreateDatetime
		,CCN_UPD.CCN_USERNAME CcnUsernameUpdate
		,DHT_UPDATE.DHT_DATETIME DtgUpdateDatetime
		,RLS_CODE RlsCode
		,RLS_VERSION RlsVersion
		,RLS_REVISION RlsRevision
		,RLS_LIVE_FLAG RlsLiveFlag
		,RLS_LIVE_DATETIME_FROM RlsLiveDatetimeFrom
		,RLS_LIVE_DATETIME_TO RlsLiveDatetimeTo
		,RLS_EXCEPTIONAL_FLAG RlsExceptionalFlag
		,RLS_RESERVATION_FLAG RlsReservationFlag
		,RLS_ARCHIVE_FLAG RlsArchiveFlag
		,RLS_ANALYTICAL_FLAG RlsAnalyticalFlag
		,LNG_ISO_CODE LngIsoCode
		,LNG_ISO_NAME LngIsoName
		,CPR_VALUE CprValue
		,CPR_CODE CprCode
		,FRQ_VALUE FrqValue
		,FRQ_CODE FrqCode
		,FRM_TYPE FrmType
		,FRM_VERSION FrmVersion
		,GRP_CODE GrpCode
		,GRP_NAME GrpName
		,GRP_CONTACT_NAME GrpContactName
		,GRP_CONTACT_PHONE GrpContactPhone
		,GRP_CONTACT_EMAIL GrpContactEmail
		,CMM_CODE CmmCode
		,CMM_VALUE CmmValue
		,SBJ_CODE SbjCode
		,SBJ_VALUE SbjValue
		,PRC_CODE PrcCode
		,PRC_VALUE PrcValue
	FROM TD_MATRIX
	INNER JOIN TD_AUDITING
		ON DTG_ID = MTR_DTG_ID
	INNER JOIN TM_AUDITING_HISTORY DHT_CREATE
		ON DTG_ID = DHT_CREATE.DHT_DTG_ID
	INNER JOIN TS_AUDITING_TYPE DTP_CREATE
		ON DTP_CREATE.DTP_ID = DHT_CREATE.DHT_DTP_ID
			AND DTP_CREATE.DTP_CODE = 'CREATED'
	INNER JOIN TD_ACCOUNT CCN_CRT
		ON CCN_CRT.CCN_ID = DHT_CREATE.DHT_CCN_ID
	LEFT JOIN TM_AUDITING_HISTORY DHT_UPDATE
		ON DTG_ID = DHT_UPDATE.DHT_DTG_ID
			AND DHT_UPDATE.DHT_DATETIME = (
				SELECT max(DHT_DATETIME)
				FROM TM_AUDITING_HISTORY
				WHERE DHT_DTG_ID = DTG_ID
				)
	LEFT JOIN TS_AUDITING_TYPE DTP_UPDATE
		ON DHT_UPDATE.DHT_DTP_ID = DTP_UPDATE.DTP_ID
			AND DTP_UPDATE.DTP_CODE = 'UPDATED'
	LEFT JOIN TD_ACCOUNT CCN_UPD
		ON CCN_UPD.CCN_ID = DHT_UPDATE.DHT_CCN_ID
	INNER JOIN TD_RELEASE
		ON RLS_ID = MTR_RLS_ID
			AND RLS_CODE = @RlsCode
			AND RLS_DELETE_FLAG = 0
	INNER JOIN @GroupUserHasAccess g
		ON g.GRP_ID = RLS_GRP_ID
	INNER JOIN TS_LANGUAGE
		ON LNG_ID = MTR_LNG_ID
			AND LNG_DELETE_FLAG = 0
	INNER JOIN TS_COPYRIGHT
		ON CPR_ID = MTR_CPR_ID
			AND CPR_DELETE_FLAG = 0
	INNER JOIN TD_FREQUENCY
		ON FRQ_MTR_ID = MTR_ID
	INNER JOIN TS_FORMAT
		ON FRM_ID = MTR_FRM_ID
	INNER JOIN TD_GROUP GP
		ON RLS_GRP_ID = GP.GRP_ID
			AND GP.GRP_DELETE_FLAG = 0
	LEFT JOIN TD_COMMENT
		ON CMM_ID = RLS_CMM_ID
			AND CMM_DELETE_FLAG = 0
	LEFT JOIN TD_PRODUCT
		ON RLS_PRC_ID = PRC_ID
			AND PRC_DELETE_FLAG = 0
	LEFT JOIN TD_SUBJECT
		ON SBJ_ID = PRC_SBJ_ID
			AND SBJ_DELETE_FLAG = 0
	WHERE MTR_DELETE_FLAG = 0
END
GO


