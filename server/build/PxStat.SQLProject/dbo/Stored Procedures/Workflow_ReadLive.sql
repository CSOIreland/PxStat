
-- =============================================
-- Author:	Neil O'Keeffe
-- Read date: 06/11/2019
-- Description:	Reads live records
-- exec Workflow_ReadLive 'okeeffene',null,'en','en'
-- =============================================
CREATE
	

 PROCEDURE Workflow_ReadLive @CcnUsername NVARCHAR(256)
	,@RlsCode INT = NULL
	,@LngIsoCode CHAR(2)
	,@LngIsoCodeDefault CHAR(2)
AS
BEGIN
	SET NOCOUNT ON;

	DECLARE @GroupUserHasAccess TABLE (GRP_ID INT NOT NULL);

	INSERT INTO @GroupUserHasAccess
	EXEC Security_Group_AccessList @CcnUsername

	DECLARE @LngId INT
	DECLARE @LngDefaultId INT

	SET @LngId = (
			SELECT LNG_ID
			FROM TS_LANGUAGE
			WHERE LNG_ISO_CODE = @LngIsoCode
				AND LNG_DELETE_FLAG = 0
			)
	SET @LngDefaultId = (
			SELECT LNG_ID
			FROM TS_LANGUAGE
			WHERE LNG_ISO_CODE = @LngIsoCodeDefault
				AND LNG_DELETE_FLAG = 0
			)

	SELECT DISTINCT RLS_CODE RlsCode
		,mtr.MTR_CODE MtrCode
		,GRP_CODE GrpCode
		,GRP_NAME GrpName
		,CCN_USERNAME CcnUsername
		,RLS_RESERVATION_FLAG RlsReservationFlag
		,RLS_ARCHIVE_FLAG RlsArchiveFlag
		,RLS_EXPERIMENTAL_FLAG RlsExperimentalFlag
		,MTR_OFFICIAL_FLAG MtrOfficialFlag
		,RLS_LIVE_DATETIME_FROM RlsLiveDatimeFrom
		,coalesce(mtrLng.MTR_TITLE, mtr.MTR_TITLE) MtrTitle
		
	FROM TD_MATRIX mtr
	LEFT JOIN (
		SELECT MTR_CODE
			,MTR_TITLE
			,MTR_RLS_ID
		FROM TD_MATRIX
		WHERE MTR_DELETE_FLAG = 0
			AND MTR_LNG_ID = @LngId
		) mtrLng
		ON mtr.MTR_CODE = mtrLng.MTR_CODE
			AND MTR.MTR_RLS_ID = mtrLng.MTR_RLS_ID
	INNER JOIN TD_RELEASE
		ON RLS_ID = mtr.MTR_RLS_ID
			AND RLS_DELETE_FLAG = 0
	INNER JOIN VW_RELEASE_LIVE_NOW
		ON VRN_RLS_ID = RLS_ID
			AND VRN_MTR_ID = MTR_ID
	INNER JOIN @GroupUserHasAccess g
		ON g.GRP_ID = RLS_GRP_ID
	INNER JOIN TD_GROUP GRP
		ON GRP.GRP_ID = RLS_GRP_ID
			AND GRP_DELETE_FLAG = 0
	INNER JOIN TM_AUDITING_HISTORY
		ON MTR_DTG_ID = DHT_DTG_ID
	INNER JOIN TD_ACCOUNT
		ON DHT_CCN_ID = CCN_ID
			AND CCN_DELETE_FLAG = 0
	LEFT JOIN TD_WORKFLOW_REQUEST
		ON RLS_ID = WRQ_RLS_ID
			AND WRQ_DELETE_FLAG = 0
	LEFT JOIN TS_REQUEST 
	ON WRQ_RQS_ID =RQS_ID
	LEFT JOIN TD_WORKFLOW_RESPONSE 
	ON WRQ_ID=WRS_WRQ_ID
	LEFT JOIN TS_RESPONSE 
	ON WRS_RSP_ID=RSP_ID  
	LEFT JOIN TD_WORKFLOW_SIGNOFF 
	ON WRS_ID=WSG_WRS_ID 
	LEFT JOIN TS_SIGNOFF 
	ON WSG_SGN_ID=SGN_ID
	WHERE MTR_DELETE_FLAG = 0
		AND (
			@RlsCode IS NULL
			OR @RlsCode = RLS_CODE
			)
		AND mtr.MTR_LNG_ID IN (
			@LngId
			,@LngDefaultId
			)
	ORDER BY mtr.MTR_CODE
END
